plugins {
    id 'java-application-conventions'
    id 'org.springframework.boot' version '3.0.1' apply false
    id 'io.spring.dependency-management' version '1.1.0'
}

dependencyManagement {
    imports {
        mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
    }
}

dependencies {
    implementation project(':domain')
    implementation project(':infrastructure')
    implementation project(':api')

    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.2'
    implementation 'org.flywaydb:flyway-core'
    implementation 'com.h2database:h2'
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

testCodeCoverageReport {
    getClassDirectories().setFrom(files(
            [project(':api'),
             project(':infrastructure'),
            ].collect {
                it.fileTree(dir: "${it.buildDir}/classes/java/main", exclude: [
                        '**/client/**',
                        '**/repository/**',
                        '**/Application'
                ])
            }
    ))
}
